apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'

apply from: rootProject.file('gradle/gradle-mvn-push.gradle')
afterEvaluate {
    generateReleaseBuildConfig.enabled = false
}

android {
    compileSdkVersion versions.compileSdk
    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.compileSdk
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    lintChecks project(':moduleEnforcer-lint')
}

task copyLibs(type: Copy) {
    group 'build'
    description 'Copies the lintCheck dependencies into the Libs directory'
    // We need to do this to make annotations from the lint library available to users.
    // based on: https://stackoverflow.com/a/33539941/6526330
    // According to the comment in this medium post I may be able to improve this with modules:
    // https://android.jlelse.eu/writing-custom-lint-rules-and-integrating-them-with-android-studio-inspections-or-carefulnow-c54d72f00d30
    from configurations.lintChecks
    into 'libs'
}
build.dependsOn copyLibs
